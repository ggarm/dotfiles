# $Id: PKGBUILD 128346 2011-06-23 08:55:12Z ibiru $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgbase=mplayer
pkgname=('mplayer' 'mencoder')
pkgver=34007
pkgrel=1
arch=('i686' 'x86_64')
#depends=( 'ttf-dejavu' 'xvidcore' )
makedepends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl' 'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient'
'aalib' 'libcaca' 'x264' 'faac' 'faad2' 'libxvmc' 'enca' 'libvdpau' 'libdca' 'a52dec' 'schroedinger' 'libvpx'
'libpulse' 'unzip' 'mesa' 'yasm' 'git' 'fontconfig' 'mpg123' 'libass')
license=('GPL')
url="http://www.mplayerhq.hu/"
options=(!buildflags !emptydirs)
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz mplayer.desktop)
md5sums=('675b929bab9e3540bb63207ed940fb7f'
				 'c0d6ef795cf6de48e3b87ff7c23f0319')

build() {
  cd ${srcdir}/${pkgname}

  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --disable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-mga \
			--disable-ass-internal \
      --disable-jack \
      --disable-lirc \
      --disable-lircc \
      --disable-smb \
      --disable-live \
      --disable-xvmc \
      --disable-vdpau \
      --language=en \
      --confdir=/etc/mplayer
	[ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

	make
}

package_mplayer() {
	pkgdesc="A movie player for linux"
	install=mplayer.install
	backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
	depends=('desktop-file-utils' 'ttf-dejavu' 'enca' 'libxss' 'a52dec' 'libvpx' 'x264' 'libmng' 'libdca' 'aalib' 'lame' 'fontconfig'  'libgl'
	'libxinerama' 'libvdpau' 'libpulse' 'smbclient' 'xvidcore' 'opencore-amr' 'jack' 'cdparanoia' 'libmad' 'sdl' 'libtheora' 'libcaca' 'libxxf86dga'
	'libjpeg' 'faac' 'faad2' 'libxvmc' 'schroedinger' 'mpg123' 'libass' 'libxxf86vm')

	cd ${srcdir}/${pkgbase}

	make DESTDIR=${pkgdir} install-mplayer  install-mplayer-man
	install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
	install -dm755 ${pkgdir}/usr/share/mplayer/
	ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
	rm -rf ${pkgdir}/usr/share/mplayer/font
	#desktop file  FS#14770
	install -Dm644 ${srcdir}/mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop
	install -Dm644 etc/mplayer.png ${pkgdir}/usr/share/pixmaps/mplayer.png
}

package_mencoder() {
	pkgdesc="Free command line video decoding, encoding and filtering tool"
	depends=('enca' 'a52dec' 'libvpx' 'x264' 'libmng' 'libdca' 'bzip2' 'lame' 'alsa-lib' 'fontconfig' 'giflib' 'libpng' 'smbclient' 'xvidcore' 'opencore-amr' 'cdparanoia'
	'libmad' 'libtheora' 'fribidi' 'libjpeg' 'faac' 'faad2' 'schroedinger' 'mpg123' 'libass')

	cd ${srcdir}/${pkgbase}
	make DESTDIR=${pkgdir} install-mencoder install-mencoder-man
	find $pkgdir/usr/share/man -name 'mplayer.1' -exec bash -c 'mv "$1" "${1/mplayer/mencoder}"' _ {} \;
}
